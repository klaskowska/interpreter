-- Programs
entrypoints Prog ;
Prog.       Prog ::= [DeclDef] ;

-- Declarations and definitions
VarGlobDef. DeclDef ::= VarDecl ;
FuncDef.    DeclDef ::= Type Ident "(" [Arg] ")" Block ;
separator DeclDef "" ;

VarDecl.    VarDecl ::= Type Item ";" ;
NoInit.     Item ::= Ident ;
Init.       Item ::= Ident "=" Expr ;
ArgVal.     Arg ::= Type Ident ;
ArgRef.     Arg ::= Type "&" Ident ;
separator Arg "," ;

-- Statements
Block.      Block ::= "{" [Stmt] "}" ;
BlockStmt.  Stmt ::= Block ;
VarLocDef.  Stmt ::= VarDecl ;
Ass.        Stmt ::= Ident "=" Expr ";" ;
If.         Stmt ::= "if" "(" Expr ")" Stmt "endif" ;
IfElse.     Stmt ::= "if" "(" Expr ")" Stmt "else" Stmt "endif" ;
While.      Stmt ::= "while" "(" Expr ")" Stmt ;
CallFunc.   Stmt ::= Ident "(" [Expr] ")" ";" ;
StmtExpr.   Stmt ::= "calc" Expr ";" ;
Print.      Stmt ::= "print" "(" Expr ")" ";" ;
Ret.        Stmt ::= "return" Expr ";" ;
RetVoid.    Stmt ::= "return" ";" ;
separator Stmt "" ;

-- Expressions
EVar.       Expr6 ::= Ident ;
EInt.       Expr6 ::= Integer ;
ETrue.      Expr6 ::= "True" ;
EFalse.     Expr6 ::= "False" ;
EString.    Expr6 ::= String ;
EBracket.   Expr6 ::= "[" Expr "]" ;
ECallFunc.  Expr6 ::= Ident "(" [Expr] ")" ";" ;
ELambda.    Expr6 ::= "lambda" "(" [Arg] ")" "->" Type Block ;
Neg.        Expr5 ::= "-" Expr6 ;
Not.        Expr5 ::= "!" Expr6 ;
EMul.       Expr4 ::= Expr5 MulOp Expr4 ;
EAdd.       Expr3 ::= Expr4 AddOp Expr3 ;
ERel.       Expr2 ::= Expr3 RelOp Expr2 ;
EAnd.       Expr1 ::= Expr2 "&&" Expr1 ;
EOr.        Expr ::= Expr1 "||" Expr ;

separator Expr ",";
coercions  Expr 6 ;

-- Operators
Plus.      AddOp ::= "+" ;
Minus.     AddOp ::= "-" ;
Times.     MulOp ::= "*" ;
Div.       MulOp ::= "/" ;
Mod.       MulOp ::= "%" ;
LTH.       RelOp ::= "<" ;
LE.        RelOp ::= "<=" ;
GTH.       RelOp ::= ">" ;
GE.        RelOp ::= ">=" ;
EQU.       RelOp ::= "==" ;
NE.        RelOp ::= "!=" ;

-- Types 
Int.       Type1 ::= "int" ;
Str.       Type1 ::= "string" ;
Bool.      Type1 ::= "boolean" ;
Void.      Type1 ::= "void" ;
FuncType.  Type ::= "function" "<" Type ">" "(" [Type] ")" ;
separator Type "," ;
coercions Type 1 ;